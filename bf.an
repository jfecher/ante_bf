
global argv argc

if argc != 2 and argc != 3 then
    print "usage: bf <file>"
    print "     | bf -e <program>"
    exit 1

//read file's contents into string
var text = ""

if argc == 3 then
    //read from stdin
    text = Str (argv#2)
else
    //read from file
    var f = InFile Str(argv#1)
    for line in f do
        text = text ++ line


fun calloc: usz nItems elemSize -> void*;

var data_ptr = u8* calloc 256 1
var text_ptr = c8* text.cStr

//main interpreter loop
while @text_ptr != '\0' do
    let c = @text_ptr
    if c == '>' then
        data_ptr += 1

    elif c == '<' then
        data_ptr -= 1

    elif c == '+' then
        @data_ptr += 1u8

    elif c == '-' then
        @data_ptr -= 1u8

    elif c == '.' then
        putchar c8(@data_ptr)

    elif c == ',' then
        @data_ptr = u8 getchar()

    elif c == '[' then
        if @data_ptr == '\0' then
            var nested = 1
            text_ptr += 1
            while nested > 0 do
                let c = @text_ptr
                if c == '[' then nested += 1
                elif c == ']' then nested -=1
                text_ptr += 1

            continue

    elif c == ']' then
        if @data_ptr != '\0' then
            var nested = 1
            text_ptr -= 1
            while nested > 0 do
                let c = @text_ptr
                if c == ']' then nested += 1
                elif c == '[' then nested -= 1
                text_ptr -= 1

    text_ptr += 1

//free data_pointer
